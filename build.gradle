if (!project.hasProperty('minecraft_version')) {
    throw new GradleException('Usage: gradle build -Pminecraft_version=... ')
}

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        switch (minecraft_version) {
        case '1.6.4':
            classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
            break
        case '1.7.2':
        case '1.7.10':
        case '1.8':
            classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
            break
        default:
            throw new GradleException('Invalid Minecraft version ' + minecraft_version)
        }
    }   
}

repositories {
    maven {
        name "OpenMods"
        url "http://repo.openmods.info/artifactory/openmods"
    }
}

configurations {
    mergeJars
    
    compile {
         extendsFrom mergeJars
    }
}

dependencies {
    mergeJars "openeye:OpenEye-protocol:1.0"
}

apply plugin: 'forge'

switch (minecraft_version) {
case '1.6.4':
    ext.forge_version = '9.11.1.964'
    ext.src_extra = ['1.6.4']
    break
case '1.7.2':
    ext.forge_version = '10.12.2.1121'
    ext.src_extra = ['1.7', '1.7.2']
    break
case '1.7.10':
    ext.forge_version = '10.13.0.1152'
    ext.src_extra = ['1.7', '1.7.10']
    break
case '1.8':
    ext.forge_version = '11.14.3.1450'
    ext.src_extra = ['1.8']
    break
}


sourceSets {
    main {
        java {
            if (minecraft_version.equals('1.8'))
                srcDir "src-1.8"
            else
                srcDir "src"
            
            for (extra_src in src_extra) {
                srcDir "src-${extra_src}"
            }
        }
        resources {
            srcDir "resources"
        }
    }
}

ext.mod_version = '0.6'
ext.mod_group = 'com.openmods.openeye'

archivesBaseName = 'OpenEye'
version = "${mod_version}-${minecraft_version}"

ext.env = System.getenv()
if (env.BUILD_NUMBER != null) {
    version += "-snapshot-" + env.BUILD_NUMBER
}

processResources {
    inputs.property "version", mod_version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': mod_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

minecraft {
    version = minecraft_version + "-" + forge_version
    assetDir = "run/assets"

    replaceIn 'openeye/Mod.java'
    replace "@VERSION@", mod_version
}

jar {
    manifest {
        attributes 'FMLCorePlugin': 'openeye.CorePlugin'
    }
    
    from configurations.mergeJars.collect { it.isDirectory() ? it : zipTree(it) }
}

